DELETE  { GRAPH <http://data.nationalarchives.gov.uk/formatregistry/data/containerSignatures>{?s ?p ?o }}
WHERE
{ GRAPH <http://data.nationalarchives.gov.uk/formatregistry/data/containerSignatures> {?s ?p ?o }}
;
INSERT  { GRAPH <http://data.nationalarchives.gov.uk/formatregistry/data/containerSignatures>{
#CONSTRUCT{
	?containerSignatureIRI
		rdf:type <http://data.nationalarchives.gov.uk/formatregistry/def/ContainerSignature> ;
		<http://www.w3.org/2004/02/skos/core#broader>  <http://data.nationalarchives.gov.uk/formatregistry/def/ContainerSignature> ;
		<http://data.nationalarchives.gov.uk/formatregistry/def/containerSignature.ID>  ?containerSignatureId ;
		rdfs:comment ?containerSignatureComment ;
		rdfs:label ?containerSignatureLabel ;
		<http://www.w3.org/2004/02/skos/core#prefLabel> ?containerSignatureLabel ;
		<http://data.nationalarchives.gov.uk/formatregistry/def/containerSignature.ContainerType>  ?containerSignatureTypeIRI .
	?containerSignatureIRI <http://data.nationalarchives.gov.uk/formatregistry/def/containerSignature.ContainerFile> ?containerFileIRI .
	?containerFileIRI rdf:type <http://data.nationalarchives.gov.uk/formatregistry/def/ContainerFile> ;
		rdfs:comment ?containerFileLabel ;
		rdfs:label ?containerFileLabel ;
		<http://www.w3.org/2004/02/skos/core#prefLabel> ?containerFileLabel .
	?containerFileIRI <http://data.nationalarchives.gov.uk/formatregistry/def/containerFile.ByteSequence> ?byteSequenceIRI ;
		<http://data.nationalarchives.gov.uk/formatregistry/def/containerFile.FilePath> ?path .
	?byteSequenceIRI rdf:type <http://data.nationalarchives.gov.uk/formatregistry/def/ByteSequence> ;
		rdfs:comment ?byteSequenceLabel ;
		rdfs:label ?byteSequenceLabel ;
		<http://www.w3.org/2004/02/skos/core#prefLabel> ?byteSequenceLabel ;
		<http://data.nationalarchives.gov.uk/formatregistry/def/internalSignature.ID>  ?internalSignatureId ;
		<http://data.nationalarchives.gov.uk/formatregistry/def/byteSequence.Value> ?sequence ;
		<http://data.nationalarchives.gov.uk/formatregistry/def/byteSequence.ByteSequencePosition> ?positionType ;
		<http://data.nationalarchives.gov.uk/formatregistry/def/byteSequence.Position> ?position ;
		#<http://data.nationalarchives.gov.uk/formatregistry/def/byteSequence.IndirectOffsetLength> 0 ;
		#<http://data.nationalarchives.gov.uk/formatregistry/def/byteSequence.IndirectOffsetLocation> 0 ;
		<http://data.nationalarchives.gov.uk/formatregistry/def/byteSequence.MaxOffset> ?subSeqMaxOffset ;
		<http://data.nationalarchives.gov.uk/formatregistry/def/byteSequence.Offset> ?subSeqMinOffset  ;
}
}
#select *
WHERE {
	?containerSignature a :ContainerSignature ;
		:ContainerType-ContainerSignature ?containerSignatureType ;
		:Id-ContainerSignature ?containerSignatureId ;
		composite:child ?descriptionChild  ;
		composite:child ?filesChild  .
	?descriptionChild a  :Description ;
	 	composite:child ?descriptionTextChild  .
	?descriptionTextChild a sxml:TextNode ;
	 	sxml:text  ?containerSignatureComment  .
	?filesChild a  :Files  ;
	 	composite:child ?fileChild  .
	?fileChild a  :File  ;
		composite:index ?fileIndex ;
	 	composite:child ?pathChild  .
	?pathChild a  :Path  ;
		composite:child ?pathTextChild  .
	?pathTextChild a  sxml:TextNode  ;
		sxml:text  ?path  .
	OPTIONAL{
		?fileChild 	composite:child ?binarySignaturesChild  .
		?binarySignaturesChild a  :BinarySignatures ;
			composite:child ?internalSignatureCollectionChild  .
		?internalSignatureCollectionChild  a :InternalSignatureCollection  ;
			composite:child ?internalSignatureChild  .
 		?internalSignatureChild  a :InternalSignature  ;
			:ID-InternalSignature  ?internalSignatureId ;
			composite:child ?byteSequenceChild .
		?byteSequenceChild  a :ByteSequence ;
			:Reference-ByteSequence ?byteOrderPosition ;
			composite:child ?subSequenceChild .
		?subSequenceChild  a :SubSequence ;
			:Position-SubSequence ?position ;
			composite:child ?SequenceChild .
		OPTIONAL{ ?subSequenceChild	:SubSeqMaxOffset-SubSequence ?subSeqMaxOffset }.
		OPTIONAL{ ?subSequenceChild	:SubSeqMinOffset-SubSequence ?subSeqMinOffset }.
		?SequenceChild a :Sequence ;
			composite:child ?SequenceTextChild .
		?SequenceTextChild  a sxml:TextNode  ;
			sxml:text  ?sequence .
	}
	BIND( IRI(CONCAT("http://data.nationalarchives.gov.uk/formatregistry/id/ContainerSignature/CS-",  STR(?containerSignatureId))) as ?containerSignatureIRI)
	BIND(CONCAT("CF-",  STR(?containerSignatureId),"-", STR(?fileIndex)) as ?containerFileLabel)
	BIND( IRI(CONCAT("http://data.nationalarchives.gov.uk/formatregistry/id/ContainerFile/",  ?containerFileLabel)) as ?containerFileIRI)
	BIND( IRI(CONCAT("http://data.nationalarchives.gov.uk/formatregistry/id/ContainerType/CT-",  STR(?containerSignatureType))) as ?containerSignatureTypeIRI)
	BIND ( ?containerSignatureComment as ?containerSignatureLabel )
	BIND ( CONCAT("BS-",STR(?containerSignatureId),"-", STR(?internalSignatureId),"-", STR(?position) )as ?byteSequenceLabel)
	BIND( IRI(CONCAT("http://data.nationalarchives.gov.uk/formatregistry/id/ByteSequence/",?byteSequenceLabel)) as ?byteSequenceIRI)
	BIND(
	IF(?byteOrderPosition = "BOFoffset", <http://data.nationalarchives.gov.uk/formatregistry/id/ByteSequencePosition/AbsoluteBOF>,
	IF(?byteOrderPosition = "EOFoffset", <http://data.nationalarchives.gov.uk/formatregistry/id/ByteSequencePosition/AbsoluteEOF>,
	"")) as ?positionType)
}
;
#Construct containerTypes
DELETE  { GRAPH <http://data.nationalarchives.gov.uk/formatregistry/data/containerTypes>{?s ?p ?o }}
WHERE
{ GRAPH <http://data.nationalarchives.gov.uk/formatregistry/data/containerTypes> {?s ?p ?o }}
;
INSERT  { GRAPH <http://data.nationalarchives.gov.uk/formatregistry/data/containerTypes>{
#CONSTRUCT{
	?containerTypeIRI
		rdf:type <http://data.nationalarchives.gov.uk/formatregistry/def/ContainerType> ;
		<http://www.w3.org/2004/02/skos/core#broader>  <http://data.nationalarchives.gov.uk/formatregistry/def/ContainerType> ;
		rdfs:comment ?containerType ;
		rdfs:label ?containerType ;
		<http://www.w3.org/2004/02/skos/core#prefLabel> ?containerType ;
		<http://data.nationalarchives.gov.uk/formatregistry/def/containerType.FileFormat>  ?puidIRI  .
}
}
#select *
WHERE {
	?triggerPuid a :TriggerPuid ;
		:ContainerType-TriggerPuid ?containerType ;
		:Puid-TriggerPuid ?puidLabel ;
		BIND( IRI(CONCAT("http://www.nationalarchives.gov.uk/PRONOM/",  ?puidLabel)) as ?puidIRI)
		BIND( IRI(CONCAT("http://data.nationalarchives.gov.uk/formatregistry/id/ContainerType/CT-",  STR(?containerType))) as ?containerTypeIRI)
}
;

#Construct fileFormatMappings
DELETE  { GRAPH <http://data.nationalarchives.gov.uk/formatregistry/data/fileFormatMappings>{?s ?p ?o }}
WHERE
{ GRAPH <http://data.nationalarchives.gov.uk/formatregistry/data/fileFormatMappings> {?s ?p ?o }}
;
INSERT  { GRAPH <http://data.nationalarchives.gov.uk/formatregistry/data/fileFormatMappings>{
#CONSTRUCT{
	?containerSignatureIRI
		<http://data.nationalarchives.gov.uk/formatregistry/def/containerSignature.FileFormat>  ?puidIRI  .
}
}
#select *
WHERE {
	?fleFormatMapping a :FileFormatMapping;
		:Puid-FileFormatMapping ?puidLabel ;
		:signatureId-FileFormatMapping ?signatureId  ;
		BIND( IRI(CONCAT("http://www.nationalarchives.gov.uk/PRONOM/",  ?puidLabel)) as ?puidIRI)
		BIND( IRI(CONCAT("http://data.nationalarchives.gov.uk/formatregistry/id/ContainerSignature/CS-",  STR(?signatureId))) as ?containerSignatureIRI)
}
;